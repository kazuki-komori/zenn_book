---
title: "Bicep ã‚’ä½¿ã£ã¦ Azure App Service ã‚’ Private Endpoint ã«ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹æ–¹æ³•"
emoji: "ğŸ’ª"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["azure", "bicep", "appservice"]
published: true
---

# ã¯ã˜ã‚ã«

Bicep ã¯ã€å®£è¨€å‹ã®æ§‹æ–‡ã‚’ä½¿ç”¨ã—ã¦ Azure ãƒªã‚½ãƒ¼ã‚¹ã‚’å®šç¾©ã™ã‚‹ãŸã‚ã®ãƒ‰ãƒ¡ã‚¤ãƒ³å›ºæœ‰è¨€èªï¼ˆDSLï¼‰ã§ã™ã€‚Bicep ã‚’ä½¿ã†ã¨ã€IaC (Infrastructure as Code) ã«ã‚ˆã‚Šä¸€è²«æ€§ã®ã‚ã‚‹ãƒ‡ãƒ—ãƒ­ã‚¤ãŒå¯èƒ½ã«ãªã‚Šã¾ã™ã€‚
ä»Šå›ã¯ã€Bicep ã‚’ä½¿ã£ã¦ Azure App Service ã‚’é–‰åŸŸç¶²ã¨ã—ã¦ Private Endpoint ã«ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹æ–¹æ³•ã‚’ç´¹ä»‹ã—ã¾ã™ã€‚

https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/overview

# ã‚¢ãƒ¼ã‚­ãƒ†ã‚¯ãƒãƒ£æ§‹æˆ

ä»Šå›ã®ã‚¢ãƒ¼ã‚­ãƒ†ã‚¯ãƒãƒ£ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

![Architecture image](/images/20230822_app_service_private_endpoint/architecture.png)

App Service ã¯ã€Private Endpoint ã¨ã—ã¦ãƒ‡ãƒ—ãƒ­ã‚¤ã•ã‚Œã€ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã‹ã‚‰ã®ã‚¢ã‚¯ã‚»ã‚¹ã‚’æ‹’å¦ã—ã¾ã™ã€‚ã•ã‚‰ã«ã€Private DNS Zone ã«ã‚ˆã‚Šã€ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯å†…ã§åå‰è§£æ±ºã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

# äº‹å‰æº–å‚™

æœ€æ–°ã® [Azure Developer CLI (azd)](https://learn.microsoft.com/ja-jp/azure/developer/azure-developer-cli/reference) ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã¦ãŠãã¾ã—ã‚‡ã†ã€‚
ã¾ãŸã€VSCode ç”¨ã® [Bicep æ‹¡å¼µæ©Ÿèƒ½](https://marketplace.visualstudio.com/items?WT.mc_id=AZ-MVP-5003113&itemName=ms-azuretools.vscode-bicep)ã‚’ä½¿ã†ã¨ã€è£œå®Œã‚„æ§‹æ–‡ãƒã‚§ãƒƒã‚¯ãŒã§ãã‚‹ã®ã§ä¾¿åˆ©ã§ã™ã€‚

```bash
$ azd version
> azd version 1.2.0
```

# ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã®ä½œæˆ

`azd init` ã‚³ãƒãƒ³ãƒ‰ã‚’ä½¿ã£ã¦ã€`Starter - Bicep` ã‚’é¸æŠã™ã‚‹ã¨æ—¢å®šã® Bicep ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã‚’ä½œæˆã—ã¦ãã‚Œã¾ã™ã€‚

```bash
$ azd init

? Select a project template:  [Use arrows to move, type to filter]
  Minimal
> Starter - Bicep
  Starter - Terraform
  React Web App with C# API and MongoDB
  React Web App with C# API and SQL Database
  React Web App with Java API and MongoDB
  React Web App with Node.js API and MongoDB
```

åˆæœŸåŒ–ã™ã‚‹ã¨ä»¥ä¸‹ã®ã‚ˆã†ãªãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªæ§‹æˆã§ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆãŒä½œæˆã•ã‚Œã¾ã™ã€‚
```bash
$ tree bicep-project

bicep-project/
â”œâ”€â”€ README.md
â”œâ”€â”€ azure.yaml
â””â”€â”€ infra
    â”œâ”€â”€ abbreviations.json
    â”œâ”€â”€ main.bicep
    â””â”€â”€ main.parameters.json
```

# ãƒªã‚½ãƒ¼ã‚¹ã®å®šç¾©

Bicep ã¯ã€å„ãƒªã‚½ãƒ¼ã‚¹ã‚’ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨ã—ã¦å®šç¾©ã™ã‚‹ã“ã¨ã§ã€ãƒªã‚½ãƒ¼ã‚¹ã®å†åˆ©ç”¨æ€§ã‚’é«˜ã‚ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚
ä»Šå›ã¯ã€`./infra/resource` ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã«å„ãƒªã‚½ãƒ¼ã‚¹ã‚’å®šç¾©ã—ã€`main.bicep` ã§å‚ç…§ã™ã‚‹ã‚ˆã†ã«ã—ã¾ã™ã€‚

## VNET ã®ä½œæˆ

ã¾ãšã¯ã€VNET ã‚’ä½œæˆã—ã¾ã™ã€‚`./infra/resource/vnet.bicep` ã‚’ä½œæˆã—ã€ä»¥ä¸‹ã®ã‚ˆã†ã«å®šç¾©ã—ã¾ã™ã€‚ãƒ‘ãƒ©ãƒ¡ãƒ¼ã‚¿ã§ `addressPrefix` ã¨ã—ã¦ã‚¢ãƒ‰ãƒ¬ã‚¹ç©ºé–“ã‚’å—ã‘å–ã‚Šã€å†åˆ©ç”¨æ€§ã‚’é«˜ã‚ã¦ã„ã¾ã™ã€‚

```bicep:./infra/resource/vnet.bicep
param location string = resourceGroup().location
param name string
// ä½œæˆã™ã‚‹vnetã®ã‚¢ãƒ‰ãƒ¬ã‚¹ç©ºé–“
param addressPrefix string

resource vnet 'Microsoft.Network/virtualNetworks@2021-02-01' = {
  name: name
  location: location
  properties: {
    addressSpace: {
      addressPrefixes: [addressPrefix]
    }
  }
}

output name string = vnet.name
output id string = vnet.id
```

## ã‚µãƒ–ãƒãƒƒãƒˆã®ä½œæˆ

æ¬¡ã«ã€å…ˆã»ã©ä½œæˆã—ãŸ VNET ã«ç´ã¥ãã‚µãƒ–ãƒãƒƒãƒˆã‚’ä½œæˆã™ã‚‹ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’å®šç¾©ã—ã¾ã™ã€‚
VNET ã«å¯¾ã—ã¦ `existing` ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã‚’æŒ‡å®šã—ã€æ—¢å­˜ã® VNET ã®åå‰ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§ã€æ—¢å­˜ã®ãƒªã‚½ãƒ¼ã‚¹ã‚’å‚ç…§ã§ãã¾ã™ã€‚
ã¾ãŸã€å‚ç…§å…ˆã® subnet ã§ `parent` ãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã«è¦ªãƒªã‚½ãƒ¼ã‚¹ã® VNET ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§ã€è¦ªå­é–¢ä¿‚ã‚’å®šç¾©ã§ãã¾ã™ã€‚

```bicep:./infra/resource/subnet.bicep
param name string
param addressPrefix string
param existingVirtualNetworkName string

// æ—¢ã«å­˜åœ¨ã™ã‚‹ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã‚’å‚ç…§
resource existingVirtualNetwork 'Microsoft.Network/virtualNetworks@2023-04-01' existing = {
  scope: resourceGroup()
  name: existingVirtualNetworkName
}

resource subnet 'Microsoft.Network/virtualNetworks/subnets@2023-04-01' = {
  parent: existingVirtualNetwork
  name: name
  properties: {
    addressPrefix: addressPrefix
  }
}

output id string = subnet.id
output name string = subnet.name
```

## App Service Plan ã®ä½œæˆ

App Service Plan ã¯ã€App Service ã®å®Ÿè¡Œç’°å¢ƒã‚’å®šç¾©ã™ã‚‹ãƒªã‚½ãƒ¼ã‚¹ã§ã™ã€‚
å®Ÿè¡Œã™ã‚‹ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã®è¦æ¨¡ã«å¿œã˜ã¦ã€ã‚µã‚¤ã‚ºã‚„ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹æ•°ã‚’å¤‰æ›´ã§ãã¾ã™ã€‚

```bicep:./infra/resource/app-service-plan.bicep
param name string
param location string = resourceGroup().location
param sku object
param kind string = ''

resource appServicePlan 'Microsoft.Web/serverfarms@2022-09-01' = {
  name: name
  location: location
  sku: sku
  kind: kind
}

output id string = appServicePlan.id
output name string = appServicePlan.name
```

## App Service ã®ä½œæˆ

App Service ã¯ã€Web ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã‚„ API ã‚’ãƒ›ã‚¹ãƒˆã™ã‚‹ãŸã‚ã® PaaS ã‚µãƒ¼ãƒ“ã‚¹ã§ã™ã€‚
App Service ã¯ã€App Service Plan ã«ç´ã¥ã„ã¦ã„ã‚‹ãŸã‚ã€App Service Plan ã®åå‰ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§ã€App Service Plan ã‚’å‚ç…§ã§ãã¾ã™ã€‚
ã¾ãŸã€`publicNetworkAccess` ã« `Enabled` ã¾ãŸã¯ `Disabled` ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§ã€ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã‹ã‚‰ã®ã‚¢ã‚¯ã‚»ã‚¹ã‚’è¨±å¯ã™ã‚‹ã‹ã©ã†ã‹ã‚’æŒ‡å®šã§ãã¾ã™ã€‚

```bicep:./infra/resource/app-service.bicep
param name string
param location string = resourceGroup().location
param existingAppServicePlanName string
@allowed(['Enabled', 'Disabled'])
param publicNetworkAccess string

resource existingAppServicePlan 'Microsoft.Web/serverfarms@2022-09-01' existing = {
  scope: resourceGroup()
  name: existingAppServicePlanName
}

resource appService 'Microsoft.Web/sites@2022-09-01' = {
  name: name
  location: location
  properties: {
    serverFarmId: existingAppServicePlan.id
    publicNetworkAccess: publicNetworkAccess
  }
}

output id string = appService.id
output name string = appService.name
```

## Private Endpoint ã¨ private DNS Zone ã®ä½œæˆ

Private Endpoint ã¯ã€Azure ãƒªã‚½ãƒ¼ã‚¹ã«ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ã‚’å‰²ã‚Šå½“ã¦ã‚‹ã“ã¨ãŒã§ãã‚‹ãƒªã‚½ãƒ¼ã‚¹ã§ã™ã€‚
Private DNS Zone ã¯ã€Private Endpoint ã§å‰²ã‚Šå½“ã¦ãŸãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ã‚’ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯å†…ã§åå‰è§£æ±ºã™ã‚‹ãŸã‚ã® DNS ã‚¾ãƒ¼ãƒ³ã§ã™ã€‚

ã“ã“ã§ã€DNS Zone åã¯ `privatelink.{ãƒªã‚½ãƒ¼ã‚¹ãŒãƒ‡ãƒ—ãƒ­ã‚¤ã•ã‚Œã‚‹URL}` ã‚’æŒ‡å®šã—ã¾ã™ã€‚
ã¤ã¾ã‚Šã€App Service ã®å ´åˆã€ãƒ‡ãƒ—ãƒ­ã‚¤å…ˆã® URL ã¯ `azurewebsites.net` ã¨ãªã‚‹ãŸã‚ã€DNS Zone åã¯ `privatelink.azurewebsites.net` ã‚’æŒ‡å®šã—ã¾ã™ã€‚

```bicep:./infra/resource/private-endpoint.bicep
param location string = resourceGroup().location
param dnsZoneName string
param linkVnetId string
param name string
param subnetId string
param privateLinkServiceId string
param privateLinkServiceGroupIds array

// private DNS zoneã®ä½œæˆ
resource privateDnsZone 'Microsoft.Network/privateDnsZones@2020-06-01' = {
  name: 'privatelink.${dnsZoneName}'
  location: 'global'
}

// private DNS zoneã‚’ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã«ç´ä»˜ã‘ã‚‹
resource virtualNetworkLinks 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = {
  name: 'vnet-link-${name}'
  location: 'global'
  parent: privateDnsZone
  properties: {
    registrationEnabled: false
    virtualNetwork: {
      id: linkVnetId
    }
  }
}

// private DNS zoneã‚’å…¥ã‚Œã‚‹ãŸã‚ã®group
resource privateDnsZoneGroup 'Microsoft.Network/privateEndpoints/privateDnsZoneGroups@2023-04-01' = {
  parent: privateEndpoint
  name: privateDnsZone.name
  properties: {
    privateDnsZoneConfigs: [
      {
        name: 'private-link-${name}'
        properties: {
          privateDnsZoneId: privateDnsZone.id
        }
      }
    ]
  }
}

// private endpointã®ä½œæˆ
resource privateEndpoint 'Microsoft.Network/privateEndpoints@2023-02-01' = {
  name: '${name}-endpoint'
  location: location
  properties: {
    subnet: {
      id: subnetId
    }
    privateLinkServiceConnections: [
      {
        name: '${name}-connection}'
        properties: {
          privateLinkServiceId: privateLinkServiceId
          groupIds: privateLinkServiceGroupIds
        }
      }
    ]
  }
}

output privateEndpointId string = privateEndpoint.id
output privateEndpointName string = privateEndpoint.name
```

ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆã‚¨ãƒ³ãƒ‰ãƒã‚¤ãƒ³ãƒˆã‚’ä½œæˆã™ã‚‹ä¸Šã§ã€`privateLinkServiceConnections[0].groupIds` ã‚’æŒ‡å®šã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ãŒã€ã“ã‚Œã¯ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆã‚¨ãƒ³ãƒ‰ãƒã‚¤ãƒ³ãƒˆã¨ã—ã¦ãƒ‡ãƒ—ãƒ­ã‚¤ã—ãŸã„å¯¾è±¡ã®ãƒªã‚½ãƒ¼ã‚¹ã”ã¨ã«å›ºæœ‰ã®å€¤ãŒå®šã‚ã‚‰ã‚Œã¦ãŠã‚Šã€ä»¥ä¸‹ã®ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‹ã‚‰ç¢ºèªã§ãã¾ã™ã€‚

https://github.com/MicrosoftDocs/azure-docs/blob/main/articles/private-link/private-endpoint-overview.md

## main.bicep ã«å„ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’èª­ã¿è¾¼ã‚€

ã“ã“ã¾ã§ã§ã€å„ãƒªã‚½ãƒ¼ã‚¹ã® Bicep ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã§ããŸã®ã§ã€main.bicep ã«å„ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’èª­ã¿è¾¼ã¿ã¾ã™ã€‚

```bicep:./infra/main.bicep
targetScope = 'subscription'

param resourceGroupName string = '<ãƒªã‚½ãƒ¼ã‚¹ã‚°ãƒ«ãƒ¼ãƒ—å>'
param location string = 'japaneast'
param appServiceName string = '<App Serviceå>'

resource resourceGroup 'Microsoft.Resources/resourceGroups@2022-09-01' = {
  name: resourceGroupName
  location: location
}

module vnet 'resource/vnet.bicep' = {
  name: 'vnet'
  scope: resourceGroup
  params: {
    name: 'vnet-private-endpoint'
    addressPrefix: '10.0.0.0/16'
    location: location
  }
}

module appServiceSubnet 'resource/subnet.bicep' = {
  name: 'app-service-subnet'
  scope: resourceGroup
  params: {
    name: 'subnet-app-service'
    addressPrefix: '10.0.0.0/24'
    existingVirtualNetworkName: vnet.outputs.name
  }
}

module appServicePlan 'resource/app-service-plan.bicep' = {
  name: 'app-service-plan'
  scope: resourceGroup
  params: {
    name: 'ase-${appServiceName}'
    location: location
    sku: {
      name: 'S1'
      capacity: 1
    }
    kind: 'linux'
  }
}

module appService 'resource/app-service.bicep' = {
  name: 'app-service'
  scope: resourceGroup
  params: {
    name: 'app-${appServiceName}'
    location: location
    existingAppServicePlanName: appServicePlan.outputs.name
    publicNetworkAccess: 'Disabled'
  }
}

module appServicePrivateEndpoint 'resource/private-endpoint.bicep' = {
  name: 'app-service-private-endpoint'
  scope: resourceGroup
  params: {
    name: appService.outputs.name
    location: location
    subnetId: appServiceSubnet.outputs.id
    privateLinkServiceId: appService.outputs.id
    privateLinkServiceGroupIds: ['sites']
    dnsZoneName: 'azurewebsites.net'
    linkVnetId: vnet.outputs.id
  }
}
```

# ãƒ‡ãƒ—ãƒ­ã‚¤

`azd up` ã‚’å®Ÿè¡Œã—ã¦ã€ãƒªã‚½ãƒ¼ã‚¹ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã¾ã™ã€‚

```bash
$ azd up

  (âœ“) Done: Resource group: rg-bicep-private-endpoint-sample
  (âœ“) Done: Virtual Network: vnet-private-endpoint
  (âœ“) Done: App Service plan: ase-bicep-private-endpoint-sample
  (âœ“) Done: App Service: app-bicep-private-endpoint-sample
Deploying services (azd deploy)
SUCCESS: Your application was provisioned and deployed to Azure in 2 minutes 45 seconds.
```

ãƒ‡ãƒ—ãƒ­ã‚¤å¾Œã€App Service ã® URL ã«ãƒ–ãƒ©ã‚¦ã‚¶ã‹ã‚‰ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹ã¨ã€403 ã‚¨ãƒ©ãƒ¼ã‚’ç¢ºèªã§ãã‚‹ã§ã—ã‚‡ã†ã€‚

![403 image](/images/20230822_app_service_private_endpoint/403.png)

ã¾ãŸã€Azure ãƒãƒ¼ã‚¿ãƒ«ã§ä½œæˆã•ã‚ŒãŸ App Service ã¸ã‚¢ã‚¯ã‚»ã‚¹ã—ã€`ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯` ã‚’é¸æŠã™ã‚‹ã¨ `ã‚¢ã‚¯ã‚»ã‚¹åˆ¶é™`ã€`ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ ã‚¨ãƒ³ãƒ‰ãƒã‚¤ãƒ³ãƒˆ` ãŒ `ã‚ªãƒ³` ã«ãªã£ã¦ã„ã‚‹ã®ãŒç¢ºèªã§ãã¾ã™ã€‚

![Alt text](/images/20230822_app_service_private_endpoint/app-service-private.png)

# ãƒªã‚½ãƒ¼ã‚¹ã®å‰Šé™¤

`azd down` ã‚’å®Ÿè¡Œã—ã¦ã€å…¨ã¦ã®ãƒªã‚½ãƒ¼ã‚¹ã‚’å‰Šé™¤ã—ã¾ã™ã€‚
```bash
$ azd down

? Total resources to delete: 12, are you sure you want to continue? Yes
Deleting your resources can take some time.

  (âœ“) Done: Deleting resource group: rg-bicep-private-endpoint-sample


SUCCESS: Your application was removed from Azure in 3 minutes 26 seconds.
```

# ãŠã¾ã‘ï¼šä»®æƒ³ãƒã‚·ãƒ³ã‚’ä½œæˆã—ã¦ã€App Service ã«ã‚¢ã‚¯ã‚»ã‚¹ã—ã¦ã¿ã‚‹

App Service ãŒä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯å†…ã‹ã‚‰è¦‹ã‚‰ã‚Œã‚‹ã‹ã‚’ç¢ºèªã™ã‚‹ãŸã‚ã«ã€ä»®æƒ³ãƒã‚·ãƒ³ã‚’ä½œæˆã—ã¦ã€App Service ã«ã‚¢ã‚¯ã‚»ã‚¹ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚
ä»®æƒ³ãƒã‚·ãƒ³ã‚’ä½œæˆã™ã‚‹æ‰‹é †ã‚’ Bicep ã§ä½œæˆã—ã¦ã‚‚è‰¯ã‹ã£ãŸã®ã§ã™ãŒã€æ‰‹é †ãŒé•·ããªã‚‹ãŸã‚ã€ä»Šå›ã¯ Azure ãƒãƒ¼ã‚¿ãƒ«ã‹ã‚‰ä½œæˆã—ã¾ã™ã€‚ï¼ˆéœ€è¦ãŒã‚ã‚Šãã†ã§ã‚ã‚Œã°ã€åˆ¥é€”è¨˜äº‹ã‚’æ›¸ãã¾ã™ï¼‰

ä»¥ä¸‹ã®ã‚ˆã†ã«ã€App Service ã®å±ã—ã¦ã„ã‚‹ã‚µãƒ–ãƒãƒƒãƒˆã« Windows11 ä»®æƒ³ãƒã‚·ãƒ³ã‚’ä½œæˆã—ã¾ã™ã€‚

![app service private endpoint](/images/20230822_app_service_private_endpoint/vm.png)

ãƒªãƒ¢ãƒ¼ãƒˆãƒ‡ã‚¹ã‚¯ãƒˆãƒƒãƒ—ã§ä»®æƒ³ãƒã‚·ãƒ³ã«æ¥ç¶šã—ã€ãƒ–ãƒ©ã‚¦ã‚¶ã‹ã‚‰ App Service ã® URL ã«ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹ã¨ã€App Service ã®åˆæœŸç”»é¢ãŒè¡¨ç¤ºã•ã‚Œã‚‹ã®ã‚’ç¢ºèªã§ãã¾ã™ã€‚

![app service remote desktop image](/images/20230822_app_service_private_endpoint/vm-access.png)

# ã¾ã¨ã‚

ä»Šå›ã¯ã€Bicep ã‚’ä½¿ã£ã¦ Azure App Service ã‚’ Private Endpoint ã«ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹æ–¹æ³•ã‚’ç´¹ä»‹ã—ã¾ã—ãŸã€‚
Bicep ã§ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ä¸Šã§ç‰¹æœ‰ã®å¤‰æ•°ãŒã‚ã£ãŸã‚Šã™ã‚‹ã®ã§ã€ã‚¤ãƒã‹ã‚‰èª¿æŸ»ã™ã‚‹ã¨å¤§å¤‰ã‹ã¨æ€ã„ã¾ã™ã€‚ä»Šå›ã®è¨˜äº‹ãŒå°‘ã—ã§ã‚‚å‚è€ƒã«ãªã‚Œã°å¹¸ã„ã§ã™ã€‚

# å‚è€ƒ

https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/overview?tabs=bicep

https://learn.microsoft.com/ja-jp/azure/app-service/networking/private-endpoint

https://learn.microsoft.com/ja-jp/azure/dns/private-dns-privatednszone
